apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply from: 'maven-override.gradle.kts'

repositories {
    google()
    mavenCentral()
}

android {
    compileSdkVersion rootProject.ext.sdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    ndkVersion rootProject.ext.ndkVersion

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion rootProject.ext.sdkVersion

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86_64'
            moduleName "cardioRecognizer"
        }
        externalNativeBuild {
            ndkBuild {
                arguments "USE_WHOLE_OPENCV=" + rootProject.ext.useWholeOpenCV
            }
        }
        buildConfigField("Boolean", "USE_WHOLE_OPENCE", "${rootProject.ext.useWholeOpenCV}")

        consumerProguardFiles 'proguard.cfg'

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    resourcePrefix 'cio_'

    externalNativeBuild {
        ndkBuild {
            path 'src/main/jni/Android.mk'
        }
    }

    packagingOptions {
        exclude '**/libopencv_java4.so'
    }
}


tasks.withType(Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.robolectric:robolectric:3.1.2"
}